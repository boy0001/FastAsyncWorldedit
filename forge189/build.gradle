buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {url = "https://oss.sonatype.org/content/repositories/snapshots/"}
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(':core')
    compile 'org.spongepowered:spongeapi:4.+'
    compile 'org.mcstats.sponge:metrics:R8-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-forge-mc1.8.9:6.1.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        name = 'forge'
        url = 'http://files.minecraftforge.net/maven'
    }
    maven {
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        name = "Sponge Metrics"
        url = "http://repo.mcstats.org/content/repositories/releases/"
    }
}
minecraft {
    version = "1855"
    mappings = "stable_22"
    runDir = 'run'
}

project.archivesBaseName = "${project.archivesBaseName}-mc${minecraft.version}"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version,
                'mcVersion': project.minecraft.version
        exclude 'mcmod.info'
    }
}

shadowJar {
    relocate 'org.yaml.snakeyaml', 'com.boydti.fawe.yaml'
    dependencies {
        include(dependency(':core'))
        include(dependency('org.yaml:snakeyaml:1.16'))
    }
    archiveName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDir = file '../target'
}
shadowJar.doLast {
    task ->
        ant.checksum file: task.archivePath
}


reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives deobfJar
}

build.dependsOn(shadowJar)
